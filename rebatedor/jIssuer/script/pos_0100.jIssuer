###################################################
# jIssuer Script File
# Testes Funcionais Front-End PDV
# Venda Normal
# MsgType 0200/0210
# Autor : Rcamargo
###################################################

FIELDS M=3,4,11,22,24,35,41,42,48,52,61

#
# BITS OPCIONAIS DA VENDA
#
IF GETFIELD(06)  != NULL       PUTLOG "========> BIT 06 PRESENTE - Amount, Cardholder Billing "
IF GETFIELD(10)  != NULL       PUTLOG "========> BIT 10 PRESENTE - Conversion Rate Cardholder Billing "
IF GETFIELD(15)  != NULL       PUTLOG "========> BIT 15 PRESENTE - Date Settlement "
IF GETFIELD(16)  != NULL       PUTLOG "========> BIT 16 PRESENTE - Date Conversion "
IF GETFIELD(14)  != NULL       PUTLOG "========> BIT 14 PRESENTE - Date Expiration "
IF GETFIELD(23)  != NULL       PUTLOG "========> BIT 23 PRESENTE - Card Sequence Number "
IF GETFIELD(35)  != NULL       PUTLOG "========> BIT 35 PRESENTE - Track 2 "
IF GETFIELD(55)  != NULL       PUTLOG "========> BIT 55 - Chip Data PRESENTE "
IF GETFIELD(112) != NULL       PUTLOG "========> BIT 112 PRESENTE "

INITMSG 0210

PUTFIELD 02 GETFIELD(02)
PUTFIELD 03 GETFIELD(03)
PUTFIELD 04 GETFIELD(04)
PUTFIELD 10 61000000
PUTFIELD 11 GETFIELD(11)
PUTFIELD 15 DATE(MMDD)
PUTFIELD 16 DATE(MMDD)
PUTFIELD 37 RAND(6)
PUTFIELD 38 RAND(6)
PUTFIELD 39 00
PUTFIELD 41 GETFIELD(41)
PUTFIELD 48 GETFIELD(48)
PUTFIELD 49 986
PUTFIELD 63 APROVADA

#
# SE CHIP DATA PRESENTE ENTAO RESPONDE
#
IF GETFIELD(55) != NULL  PUTFIELD 55 GETFIELD(55)

#
# SE DE112
#
IF GETFIELD(112) != NULL  PUTFIELD 112 GETFIELD(112)

#
# TESTE DE VALOR / LOGICA DO VALOR
#
IF GETFIELD(4,12,1) = "1" GOTO NEG
IF GETFIELD(4,12,1) = "2" GOTO TIMEOUT
IF GETFIELD(4,12,1) = "3" GOTO TARDIA
IF GETFIELD(4,12,1) = "9" GOTO NEGTARDIA
PUTLOG " *** VALOR PARA APROVAR *** "
GOTO SEND

:NEG
PUTLOG " *** VALOR PARA NEGAR *** "
PUTFIELD 39 05
GOTO SEND

:TIMEOUT
PUTLOG " *** VALOR PARA TIMEOUT *** "
GOTO NOSEND

:TARDIA
PUTLOG " *** VALOR PARA TARDIA *** "
DELAY 20
GOTO SEND

:NEGTARDIA
PUTLOG " *** VALOR PARA NEGADA TARDIA *** "
PUTFIELD 39 05
DELAY 20
GOTO SEND

:SEND
SENDMSG

:NOSEND
# EOF