#####################################################################
# SCRIPT PARA RESPONDER UMA MENSAGEM 0210 - TICKET CHIP
# HISTORICO : VERSAO INICIAL - 30.05.2020
#####################################################################
FIELDS M=3,4,7,11,12,13,18,19,22,32,35,37,41,42,48,49,52,55

# 0200 => 3 4 7 11 12 13 18 19 22 32 35 37       41 42 48 49 52 55
# 0210 => 3 4 7 11 12 13          32    37 38 39 41 42 48 49    55 62

# 0200EST => 2 3 4 7 11 12 13 19 22 32 37       41 42 48 49    90
# 0210EST =>   3 4 7 11 12 13       32 37 38 39 41 42 48 49 62

#####################################################################
# INICIALIZACAO DA MENSAGEM
#####################################################################
INITMSG 0210

PUTFIELD 3 GETFIELD(03)
PUTFIELD 4 GETFIELD(04)
PUTFIELD 7 GETFIELD(07)
PUTFIELD 11 GETFIELD(11)
PUTFIELD 12 GETFIELD(12)
PUTFIELD 13 GETFIELD(13)
PUTFIELD 32 GETFIELD(32)
PUTFIELD 37 GETFIELD(37)
PUTFIELD 38 RAND(6)
PUTFIELD 39 00
PUTFIELD 41 GETFIELD(41)
PUTFIELD 48 GETFIELD(48)
PUTFIELD 49 GETFIELD(49)

#
# TRANSACAO DE ESTORNO NAO DEVOLVE DE55
#
IF GETFIELD(03) = 002000 THEN GOTO VENDA
PUTLOG "TRANSACAO DE ESTORNO"
GOTO SALDODISPONIVEL

:VENDA
#
# TRATAMENTO CHIP
#
IF GETFIELD(55) = NULL THEN GOTO SEMDE55
PUTLOG "DE55 FOUND"
PUTFIELD 55 911011F8CB74849000030000120700001207
:SEMDE55

:SALDODISPONIVEL
#
# VALOR DO SALDO DISPONIVEL
#
PUTFIELD 62 990024400150012000000001234

:IFVALOR
PUTLOG "Checagem de Valores"
#
# TRATAMENTO ESPECIAL PELO VALOR
#
IF GETFIELD(4,1,10) = "0000000100" THEN GOTO TRATAMENTOVALOR
IF GETFIELD(4,11,2) = "00" THEN GOTO SENDMSG
IF GETFIELD(4,11,2) = "01" THEN GOTO NEGADA
IF GETFIELD(4,11,2) = "02" THEN GOTO TARDIA
IF GETFIELD(4,11,2) = "03" THEN GOTO NEGADATARDIA
IF GETFIELD(4,11,2) = "04" THEN GOTO TIMEOUT
GOTO SENDMSG

#
# SENAO COPIA OS ULTIMOS DIGITOS DO VALOR
#
:TRATAMENTOVALOR
PUTFIELD 39 GETFIELD(4,11,2)
GOTO SENDMSG

:NEGADA
PUTFIELD 39 99
GOTO SENDMSG

:TARDIA
DELAY 20
PUTFIELD 39 00
GOTO SENDMSG

:NEGADATARDIA
DELAY 21
PUTFIELD 39 05
GOTO SENDMSG

:SENDMSG
SENDMSG

:TIMEOUT
